def repositories = ['appcenter', 'browser', 'calculator', 'files', 'terminal',
                    'text', 'settings', 'networkmanager', 'power-manager',
                    'pulseaudio', 'screencast', 'session', 'screenshot',
                    'shell']

def parallelStagesMap = repositories.collectEntries {
    ["${it}" : generateStage(it)]
}

def generateStage(job) {
    return {
        stage("Update ${job}") {
            script {
                sh label: 'Delete previous clone', script: "rm -rf ${job}"
                checkout([$class: 'GitSCM', branches: [[name: 'develop']], doGenerateSubmoduleConfigurations: false, extensions: [[$class: 'RelativeTargetDirectory', relativeTargetDir: "${job}"]], submoduleCfg: [], userRemoteConfigs: [[credentialsId: 'ci-ssh', url: "git@github.com:lirios/${job}.git"]]])
                dir("${job}") {
                    sh label: 'Set branch', script: "git checkout develop"
                    sh label: 'Push', script: '${WORKSPACE}/transifex-push .'
                    withCredentials([sshUserPrivateKey(credentialsId: 'ci-ssh', keyFileVariable: 'KEY_FILE')]) {
                        sh label: 'Pull', script: "GIT_SSH_COMMAND='ssh -i ${KEY_FILE} -o StrictHostKeyChecking=no' ${WORKSPACE}/transifex-pull ."
                    }
                }
                sh label: 'Delete clone', script: "rm -rf ${job}"
            }
        }
    }
}

pipeline {
    agent {
        docker {
            image 'liridev/ci-fedora-jenkins:31'
            alwaysPull true
        }
    }
    environment {
        CI = 1
        TX_TOKEN = credentials('transifex-token')
        QT_SELECT = 5
    }
    stages {
        stage('Preparation') {
            steps {
                sh label: 'Installation', script: '''
sudo dnf install -y transifex-client git qtchooser qt5-linguist binutils
sudo strip --remove-section=.note.ABI-tag /usr/lib64/libQt5Core.so.5
curl -O https://raw.githubusercontent.com/lirios/repotools/develop/translations/transifex-push && chmod 755 transifex-push
curl -O https://raw.githubusercontent.com/lirios/repotools/develop/translations/transifex-pull && chmod 755 transifex-pull
curl -O https://raw.githubusercontent.com/lirios/repotools/develop/translations/translate-desktop && chmod 755 translate-desktop
'''
            }
        }
        stage('Build') {
            steps {
                script {
                    parallel parallelStagesMap
                }
            }
        }
    }
}
